apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

static def buildTime() {
    return new Date().format("yyyyMMdd")
}

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        applicationId "heven.holt.kotlincompponentproject"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
        //打包时间
        resValue "string", "build_time", buildTime()
    }

    resourcePrefix "app_"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            //给applicationId添加后缀“.debug”
            applicationIdSuffix ".debug"
            //manifestPlaceholders = [app_icon: "@drawable/launch_beta"]
            buildConfigField "boolean", "LOG_DEBUG", "true"
            zipAlignEnabled false
            shrinkResources false
            minifyEnabled false
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation deps.test.junit
    androidTestImplementation deps.test.runner
    androidTestImplementation deps.test.espresso

    implementation deps.support.multidex
    implementation project(':lib_common')

    debugImplementation 'com.didichuxing.doraemonkit:doraemonkit:1.2.1'
    releaseImplementation 'com.didichuxing.doraemonkit:doraemonkit-no-op:1.2.0'

    if (!isModule.toBoolean()) {
        implementation project(':model_main')
        implementation project(':model_news')
        implementation project(':model_camera')
    }
}
